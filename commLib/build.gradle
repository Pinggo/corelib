apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.Pinggo'
android {
    compileSdkVersion 26
    defaultConfig {
//        applicationId "com.dev.pipi.commlib"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex')) {
                details.useVersion "26.1.0"
            }
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'com.android.support:appcompat-v7:26.1.0'
    api 'com.android.support:design:26.1.0'
    api 'com.android.support:cardview-v7:26.1.0'
    api 'com.android.support:recyclerview-v7:26.1.0'
    //rx
    api 'io.reactivex.rxjava2:rxjava:2.0.7'
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
    api 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.2'
    //retrofit && okhttp
    api 'com.squareup.retrofit2:retrofit:2.2.0'
    api 'com.squareup.retrofit2:converter-gson:2.2.0'
    api 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.7.0'
    //permission
    api 'pub.devrel:easypermissions:1.1.2'
    //glide
    api 'com.github.bumptech.glide:okhttp3-integration:4.0.0'
    api 'com.github.bumptech.glide:glide:4.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'
    //工具类
    api 'com.blankj:utilcode:1.20.3'
    //上下拉刷新
    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.4'
    //eventbus
    api 'org.greenrobot:eventbus:3.0.0'
    //zxing核心依赖
    api 'com.journeyapps:zxing-android-embedded:3.5.0'
    //仿微信拍照
    api 'cjt.library.wheel:camera:1.1.9'
    //greendao
    api 'org.greenrobot:greendao:3.2.2'
    //dialog
    api 'com.afollestad.material-dialogs:core:0.9.6.0'
    //知乎图片选择
    api 'com.zhihu.android:matisse:0.5.2-beta3'
}
// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
